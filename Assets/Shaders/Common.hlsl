#ifndef COMMON_INCLUDED
#define COMMON_INCLUDED

SamplerComparisonState sampler_DirectionalShadows;
Texture2D<float> _DirectionalShadows;

cbuffer PerFrameData
{
	float3 _AmbientLight;
	float _FogScale;
	
	float3 _SunDirection;
	float _FogOffset;
	
	float3 _SunColor;
	float _Time;
	
	float3 _FogColor;
	float _PerFrameDataPadding;
};

cbuffer PerViewData
{
	matrix _WorldToClip;
	matrix _WorldToShadow;
};

cbuffer PerCascadeData
{
	matrix _WorldToShadowClip;
};

#ifdef INSTANCING_ON
	cbuffer UnityDrawCallInfo
	{
		uint unity_BaseInstanceID;
	};

	cbuffer UnityInstancing_PerDraw0
	{
		struct
		{
			matrix unity_ObjectToWorldArray;
		}
	
		unity_Builtins0Array[2];
	};
#else
	cbuffer UnityPerDraw
	{
		float3x4 unity_ObjectToWorld, unity_WorldToObject;
		float4 unity_LODFade; // x is the fade value ranging within [0,1]. y is x quantized into 16 levels
		float4 unity_WorldTransformParams; // w is usually 1.0, or -1.0 for odd-negative scale transforms
	};
#endif

float3 MultiplyPoint3x4(float3x4 mat, float3 p)
{
	return p.x * mat._m00_m10_m20 + (p.y * mat._m01_m11_m21 + (p.z * mat._m02_m12_m22 + mat._m03_m13_m23));
}

float4 MultiplyPoint(float4x4 mat, float3 p)
{
	return p.x * mat._m00_m10_m20_m30 + (p.y * mat._m01_m11_m21_m31 + (p.z * mat._m02_m12_m22_m32 + mat._m03_m13_m23_m33));
}

float3 ObjectToWorld(float3 position, uint instanceId)
{
	#ifdef INSTANCING_ON
		float3x4 objectToWorld = (float3x4)unity_Builtins0Array[unity_BaseInstanceID + instanceId].unity_ObjectToWorldArray;
	#else
		float3x4 objectToWorld = unity_ObjectToWorld;
	#endif
	
	return MultiplyPoint3x4(objectToWorld, position);
}

float4 WorldToClip(float3 position)
{
	#ifdef UNITY_PASS_SHADOWCASTER
		return MultiplyPoint(_WorldToShadowClip, position);
	#else
		return MultiplyPoint(_WorldToClip, position);
	#endif
}

float4 ObjectToClip(float3 position, uint instanceId)
{
	return WorldToClip(ObjectToWorld(position, instanceId));
}

float3 ObjectToWorldNormal(float3 normal, uint instanceId)
{
	// Source: https://www.shadertoy.com/view/3s33zj
	#ifdef INSTANCING_ON
		float3x4 objectToWorld = (float3x4)unity_Builtins0Array[unity_BaseInstanceID + instanceId].unity_ObjectToWorldArray;
	#else
		float3x4 objectToWorld = unity_ObjectToWorld;
	#endif
	
	float3x3 adjoint = float3x3(cross(objectToWorld[1].xyz, objectToWorld[2].xyz), cross(objectToWorld[2].xyz, objectToWorld[0].xyz), cross(objectToWorld[0].xyz, objectToWorld[1].xyz));
	return normalize(mul(adjoint, normal));
}

float4 BilinearWeights(float2 uv, float2 textureSize)
{
	float2 localUv = frac(uv * textureSize - 0.5 + rcp(512.0));
	float4 weights = localUv.xxyy * float4(-1, 1, 1, -1) + float4(1, 0, 0, 1);
	return weights.zzww * weights.xyyx;
}

#endif